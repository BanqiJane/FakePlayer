plugins {
    id 'java'
    id 'application'
}

group 'com.ddf'
version '0.2.6-SNAPSHOT'

repositories {
    maven {
        name 'aliyun-central'
        url 'https://maven.aliyun.com/repository/central'
    }
    maven {
        name 'aliyun-public'
        url 'https://maven.aliyun.com/repository/public'
    }
    maven {
        name 'nukkitx-repo-release'
        url 'https://repo.nukkitx.com/maven-releases/'
    }
    maven {
        name 'nukkitx-repo-snapshot'
        url 'https://repo.nukkitx.com/maven-snapshots/'
    }
}

dependencies {
    implementation 'com.nukkitx.protocol:bedrock-v448:2.8.1-SNAPSHOT'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.yaml:snakeyaml:1.27'
    implementation 'com.formdev:flatlaf:1.0'
    implementation 'org.java-websocket:Java-WebSocket:1.5.1'
    implementation 'org.slf4j:slf4j-nop:1.7.25'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
mainClassName = 'com.ddf.fakeplayer.Main'

distributions {
    main {
        contents {
            from "LICENSE"
            into ""
        }
    }
}

run {
    standardInput = System.in
    jvmArgs += '-Dfakeplayer.gui=false'
}

task generateVersionInfo {
    def versionRegex = '\\$\\{VERSION\\}'
    def templateFile = new File(projectDir, "src/main/java/com/ddf/fakeplayer/VersionInfo.java.template")
    def text = templateFile.text
    text = (text =~ /${versionRegex}/).replaceAll(version.toString())
    def file = new File(projectDir, "src/main/java/com/ddf/fakeplayer/VersionInfo.java")
    file.createNewFile()
    file.write(text)
}

compileJava.dependsOn(generateVersionInfo)
compileTestJava.dependsOn(generateVersionInfo)

task runGUI(type: JavaExec) {
    classpath = run.classpath
    main = run.main
    jvmArgs += '-Dfakeplayer.gui=true'
}

startScripts {
    defaultJvmOpts = ['-Dfakeplayer.gui=false']
}

task guiStartScripts(type: CreateStartScripts) {
    applicationName = startScripts.applicationName + '-GUI'
    classpath = startScripts.classpath
    mainClassName = startScripts.mainClassName
    outputDir = startScripts.outputDir
    defaultJvmOpts = ['-Dfakeplayer.gui=true']
    doLast {
        def windowsScriptText = windowsScript.text.replaceAll('java\\.exe', 'javaw.exe')
        windowsScriptText = windowsScriptText.replaceAll('"%JAVA_EXE%" %DEFAULT_JVM_OPTS% %JAVA_OPTS%', 'start "" /B "%JAVA_EXE%" %DEFAULT_JVM_OPTS% %JAVA_OPTS%')
        windowsScript.write(windowsScriptText)
    }
}

startScripts.dependsOn(guiStartScripts)